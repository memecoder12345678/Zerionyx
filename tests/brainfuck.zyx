load "libs.sys"
load "libs.string"

memory = [0]
pointer = 0

defun pass() -> none

defun brainfuck(nput)
    if not is_str(nput) do
        panic("'nput' must be a string", "T")
    done
    nput = string.split(nput, "")
    output = ""
    i = 0
    while i < len(nput) do
        # println("i: " + to_str(i) + ", " + "nput$i: " + to_str(nput$i) + ", " + "memory: [" + to_str(memory) + "], " + "pointer: " + to_str(pointer))
        if (nput$i) == ">" do
            if pointer+1 > 30000 do
                println("Memory Error")
                sys.exit(1)
            else
                if pointer+1 >= len(memory) do
                    append(memory, 0)
                done
            done
            pointer += 1
        elif (nput$i) == "<" do
            if pointer > 0 do
                pointer -= 1
            done
        elif (nput$i) == "+" do
            set(memory, pointer, ((memory$pointer)+1) % 256)
        elif (nput$i) == "-" do
            set(memory, pointer, (memory$pointer - 1) % 256)
        elif (nput$i) == "." do
            output = output + string.chr(memory$pointer)
        elif (nput$i) == "," do
            input_str = input("> ")
            if len(input_str) > 0 do
                set(memory, pointer, string.ord(input_str$0))
            else
                set(memory, pointer, 0)
            done
        elif (nput$i) == "[" do
            if (memory$pointer) == 0 do
                count = 1
                while count > 0 do
                    i += 1
                    if (nput$i) == "[" do
                        count += 1
                    elif (nput$i) == "]" do
                        count -= 1
                    done
                done
            done
        elif (nput$i) == "]" do
            if (memory$pointer) != 0 do
                count = 1
                while count > 0 do
                    i -= 1
                    if (nput$i) == "]" do
                        count += 1
                    elif (nput$i) == "[" do
                        count -= 1
                    done
                done
            done
        else
            pass()
        done
        i += 1
    done
    return output
done

defun main()
    println("Brainfuck Interpreter (type 'quit' to exit).")
    while true do
        code = input(">> ")
        if string.strip(code, " ") == "quit" do
            sys.exit(0)
        done
        output = brainfuck(code)
        if is_none(output) do
            continue
        elif len(output) == 0 do
            continue
        else
            println(output)
        done
    done
done

if is_main do
    main()
done
